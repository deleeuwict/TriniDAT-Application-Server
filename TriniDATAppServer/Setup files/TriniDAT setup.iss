; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "TriniDAT Data Application Server"
#define MyAppVersion "1.2"
#define MyAppPublisher "De Leeuw ICT"
#define MyAppURL "http://www.deleeuwict.nl"
#define MyAppExeName "TriniDAT.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{3D495234-13A3-4889-8423-6F304D793C76}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
PrivilegesRequired=admin
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\De Leeuw ICT\{#MyAppName}
DefaultGroupName=De Leeuw ICT\TriniDAT Data Application Server
LicenseFile=C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\Documentation\Setup\license.txt
OutputDir=C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\Setup files
OutputBaseFilename=setup
SetupIconFile=C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\Resources\TriniDAT.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "Install with a &Quick Launch icon"; GroupDescription: "Additional icons:"; Flags: unchecked


[Files]
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\bin\Release\TriniDAT.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\bin\Release\*.DLL"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\trinidat_config\static\wwwroot\*"; DestDir: "{app}\trinidat_config\static\wwwroot"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\trinidat_config\simon\*.xml"; DestDir: "{app}\trinidat_config\simon\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\trinidat_config\builder\*.xml"; DestDir: "{app}\trinidat_config\builder\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\trinidat_config\speech\*.*"; DestDir: "{app}\trinidat_config\speech\"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATAppServer\trinidat_config\*.txt"; DestDir: "{app}\trinidat_config\"; 
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\TriniDATServerLicense\TriniDATServerLicense\bin\Release\TriniDATServerLicense.dll"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "C:\Users\gertjan\Documents\Visual Studio 2010\Projects\TriniDATAppServer\Setup files\TriniDAT Developer SDK.url"; DestDir: "{app}"; Flags: ignoreversion
Source: "tt0590m.ttf"; DestDir: "{fonts}"; FontInstall: "Bank Gothic Medium BT"; Flags: onlyifdoesntexist

[Dirs]
Name: "{app}\trinidat_sessions"

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"

Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{group}\Read TriniDAT .NET Development SDK"; Filename: "{app}\TriniDAT Developer SDK.url"
Name: "{commondesktop}\Read TriniDAT .NET Development SDK"; Filename: "{app}\TriniDAT Developer SDK.url"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait runascurrentuser postinstall skipifsilent

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, serviceCount: cardinal;
    success: boolean;
begin
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;
    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;
    // .NET 4.0 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;
    result := success and (install = 1) and (serviceCount >= service);
end;

function InitializeSetup(): Boolean;
begin
    if not IsDotNetDetected('v4\Client', 0) then begin
        MsgBox('TriniDAT Data Application Server requires Microsoft .NET Framework 4.0 Client Profile to be installed.'#13#13
            'Please use Windows Update to install this version,'#13
            'and then re-run the installation file.', mbInformation, MB_OK);
        result := false;
    end else
        result := true;
end;